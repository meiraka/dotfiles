local prompt_arrow="❱ "

PROMPT2=". "
SPROMPT='%{$suggest%}%B%r%b [y, n, a, e] > '
ZLE_RPROMPT_INDENT=0

function set_user_var() {
   printf "\033]1337;SetUserVar=%s=%s\007" $1 `echo -n $2 | base64`
}

local VCS=""
local VCS_REPO=""
local VCS_BRANCH=""

autoload -Uz add-zsh-hook
add-zsh-hook precmd env_vcs_info

function env_vcs_info() {
    async_stop_worker env_vcs_info
    async_start_worker env_vcs_info -n
    async_register_callback env_vcs_info env_vcs_info_callback
    async_job env_vcs_info env_vcs_info_async
}

function env_vcs_info_async() {
    if [ "$(git rev-parse --is-inside-work-tree 2> /dev/null)" = "true" ]; then
        echo "git"
        echo $(git config --get remote.origin.url)
        echo $(git rev-parse --abbrev-ref HEAD)
        return
    fi
    echo ""
    echo ""
    echo ""
}

function env_vcs_info_callback() {
    VCS=$(echo "$3" | head -n1)
    VCS_REPO=$(echo "$3" | head -n2 | tail -n1)
    VCS_BRANCH=$(echo "$3" | head -n3 | tail -n1)
    set_user_var VCS "${VCS}"
    set_user_var VCS_REPO "${VCS_REPO}"
    set_user_var VCS_BRANCH "${VCS_BRANCH}"
    update_prompt
}

local KUBE_CONTEXT=""
local KUBE_NAMESPACE=""

autoload -Uz add-zsh-hook
add-zsh-hook precmd env_kube_info

function env_kube_info() {
    async_stop_worker env_kube_info
    async_start_worker env_kube_info -n
    async_register_callback env_kube_info env_kube_info_callback
    async_job env_kube_info env_kube_info_async
}

function env_kube_info_async() {
    local context ns
    if context=$(kubectl config current-context 2>/dev/null); then
        ns=$(kubectl config view -o "jsonpath={.contexts[?(@.name==\"$context\")].context.namespace}")
        [[ -z "$ns" ]] && ns="(default)"
        echo "${context}"
        echo "${ns}"
    else
        echo ""
        echo ""
    fi
}

function env_kube_info_callback() {
    KUBE_CONTEXT=$(echo "$3" | head -n1)
    KUBE_NAMESPACE=$(echo "$3" | head -n2 | tail -n1)
    set_user_var KUBE_CONTEXT "${KUBE_CONTEXT}"
    set_user_var KUBE_NAMESPACE "${KUBE_NAMESPACE}"
}

function mode_color() {
    case $KEYMAP in
        vicmd)
            echo 'magenta'
        ;;
        main)
            echo 'blue'
        ;;
        visual)
            echo 'magenta'
        ;;
        *)
            echo 'blue'
        ;;
    esac
}
   
function zle-line-init zle-keymap-select update_prompt {
    local color="$(mode_color)"
    PROMPT="%F{${color}}${prompt_arrow}%f"
    if [ "${VCS}" != "" ]; then
        RPROMPT="${val}%F{black}%f%K{black}%F{${color}} ${VCS_REPO} %k%f%K{${color}}%F{black} ${VCS_BRANCH} %k%f"
    else
        RPROMPT=""
    fi
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select
